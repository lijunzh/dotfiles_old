# Use fd and fzf to get the args to a command.
# Works only with zsh
# Examples:
# f mv # To move files. You can write the destination after selecting the files.
# f 'echo Selected:'
# f 'echo Selected music:' --extention mp3
# fm rm # To rm files in current directory
f() {
    sels=( "${(@f)$(fd "${fd_default[@]}" "${@:2}"| fzf)}" )
    test -n "$sels" && print -z -- "$1 ${sels[@]:q:q}"
}

# fo [FUZZY PATTERN] - Open the selected file with the default editor
#	- Bypass fuzzy finder if there's only one match (--select-1)
#	- Exit if there's no match (--exit-0)
#	- CTRL-O to open with `open` command,
#	- CTRL-E or Enter key to open with the $EDITOR
fo() (
	IFS=$'\n' out=("$(fzf-tmux --query="$1" --exit-0 --expect=ctrl-o,ctrl-e)")
	key=$(head -1 <<< "$out")
	file=$(head -2 <<< "$out" | tail -1)
	if [ -n "$file" ]; then
		[ "$key" = ctrl-o ] && open "$file" || ${EDITOR:-vim} "$file"
	fi
)

# cdf - cd into the directory of the selected file
cdf() {
	local file
	local dir
	file=$(fzf-tmux +m -q "$1") && dir=$(dirname "$file") && cd "$dir"
}

##
# Interactive search.
# Usage: `ff` or `ff <folder>`.
#
ff() {
	dir=${1:-'.'}
	echo $dir
	cd $dir
	RG_DEFAULT_COMMAND="rg -i -l --hidden --no-ignore-vcs"

	selected=$(
	FZF_DEFAULT_COMMAND="rg --files" fzf-tmux \
		-m \
		-e \
		--ansi \
		--phony \
		--reverse \
		--bind "ctrl-a:select-all" \
		--bind "f12:execute-silent:(subl -b {})" \
		--bind "change:reload:$RG_DEFAULT_COMMAND {q} || true" \
		--preview "rg -i --pretty --context 2 {q} {}" | cut -d":" -f1,2
	)
	echo $selected

	cd -
	[[ -n $selected ]] && ${EDITOR:-vim} $dir/$selected # open multiple files in editor
}
